Jslt = _{ SOI ~ Expr ~ EOI }

Value = _{ Array | Object | Number | String | Boolean | Null | Accessor | FunctionCall | Scope }

Expr = _{ OperatorExpr |  Value }
OperatorExpr = { Value ~ (Operator ~ Value)+ }

Operator = _{ Add | Sub | Div | Mul | Gte | Gt | Lte | Lt | And | Or | Equal }
Add = { "+" }
Sub = { "-" }
Mul = { "*" }
Div = { "/" }
Gt = { ">" }
Lt = { "<" }
Gte = { ">=" }
Lte = { "<=" }
Equal = { "==" }
And = { "and" }
Or = { "or" }

Scope = { "(" ~ Expr ~ ")" }

Accessor = {
    "." ~ Ident? ~ KeyAccessor* ~ Accessor?
}
KeyAccessor = { "[" ~ (RangeAccessor | Expr) ~ "]" }
RangeAccessor = { Expr ~ ":" ~ Expr }

Ident = @{ !(For | If) ~ ASCII_ALPHANUMERIC ~ ("_" | "-" | ASCII_ALPHANUMERIC)* }

FunctionCall = { Ident ~ "(" ~ Expr? ~ ("," ~ Expr)* ~ ")" }

Array    = {
    "[" ~ "]"
  | "[" ~ (Expr | ArrayFor) ~ ("," ~ (Expr | ArrayFor))* ~ "]"
}
ArrayFor = {
    For ~ "(" ~ Expr ~ ")" ~ Expr ~ IfCondition?
}


If = _{ "if" }
For = _{ "for" }

Object    = {
    "{" ~ "}"
  | "{" ~ (Pair | ObjectFor) ~ ("," ~ (Pair | ObjectFor))* ~ "}"
}
ObjectFor = {
    For ~ "(" ~ Expr ~ ")" ~ Pair ~ IfCondition?
}

Pair = {
    Expr ~ ":" ~ Expr
}

IfCondition = {
    If ~ "(" ~ Expr ~ ")"
}

String = ${ "\"" ~ Inner ~ "\"" }
Inner  = @{ Char* }
Char   =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

Number = @{
    "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

Boolean = { "true" | "false" }

Null = { "null" }

COMMENT = { "//" ~ Inner ~ ("\n" | "\r" | "\r\n")? }

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
